# Machina programming language grammar

# Top-level

Module             ::= RequiresBlock? TopLevelItem*

TopLevelItem       ::= (AttributeList (TypeDef | TraitDef | FuncDecl | FuncDef))
                     | MethodBlock
                     | TypestateDef
                     | ProtocolDef

RequiresBlock      ::= "requires" "{" RequireItem* "}"
RequireItem        ::= RequirePath ("as" Identifier)? ("," | ";")?
RequirePath        ::= Identifier ("::" Identifier)*

# Attributes
AttributeList      ::= ("@" Attribute)*
Attribute          ::= Identifier ("(" StringLit ("," StringLit)* ")")?

# Type Definitions
TypeDef            ::= "type" Identifier TypeParamList? "=" TypeDefBody
TypeDefBody        ::= TypeAliasDef
                     | StructDef
                     | EnumDef

TypeAliasDef       ::=  TypeExpr ";"?

StructDef          ::= "{" StructFieldList? "}"
StructFieldList    ::= StructField ("," StructField)* ","?
StructField        ::= Identifier ":" TypeExpr

EnumDef            ::= EnumVariantDef ("|" EnumVariantDef)* ";"?
EnumVariantDef     ::= Identifier ( "(" TypeExprList ")" )?

# Traits
TraitDef           ::= "trait" Identifier "{" TraitMember* "}"
TraitMember        ::= TraitMethodDecl | TraitPropertyDecl
TraitMethodDecl    ::= MethodSig ";"
TraitPropertyDecl  ::= "prop" Identifier ":" TypeExpr "{" TraitPropertyAccessor+ "}"
TraitPropertyAccessor ::= "get" ";" | "set" ";"

# Protocols
ProtocolDef        ::= "protocol" Identifier "{" ProtocolItem* "}"
ProtocolItem       ::= ProtocolMsgDecl
                     | ProtocolReqDecl
                     | ProtocolRoleDecl
                     | ProtocolFlowDecl
ProtocolMsgDecl    ::= "msg" Identifier ( "=" TypeExpr )? ";"?
ProtocolReqDecl    ::= "req" Identifier "->" Identifier ":" TypeExpr "=>" TypeExpr ";"?
ProtocolRoleDecl   ::= "role" Identifier ( ";" | ProtocolRoleBlock )
ProtocolRoleBlock  ::= "{" ProtocolStateDecl* "}"
ProtocolStateDecl  ::= "state" Identifier ( ";" | "{" ProtocolTransition* "}" )
ProtocolTransition ::= "on" ProtocolTrigger "->" Identifier ProtocolTransitionBody
ProtocolTrigger    ::= TypeExpr ( "@" Identifier )?
ProtocolTransitionBody ::= ";" | "{" ProtocolTransitionItem* "}"
ProtocolTransitionItem ::= ProtocolEffectsDecl
ProtocolEffectsDecl ::= "effects" ":" "[" ProtocolEffectList? "]" ("," | ";")?
ProtocolEffectList ::= ProtocolEffect ( "," ProtocolEffect )* ","?
ProtocolEffect     ::= TypeExpr "~" ">" Identifier
ProtocolFlowDecl   ::= "flow" Identifier "->" Identifier ":" TypeExpr ( "->" TypeExpr )? ";"

# Typestates
TypestateDef       ::= "typestate" Identifier TypestateRoleImplList? "{" TypestateItem* "}"
TypestateRoleImplList ::= ":" TypestateRolePath ("," TypestateRolePath)*
TypestateRolePath  ::= Identifier ("::" Identifier)+
TypestateItem      ::= TypestateFields
                     | TypestateNew
                     | TypestateOnHandler
                     | (AttributeList TypestateState)
TypestateFields    ::= "fields" "{" TypestateFieldList? "}"
TypestateFieldList ::= TypestateField ("," TypestateField)* ","?
TypestateField     ::= StructField ( "as" Identifier )?
TypestateNew       ::= "fn" "new" TypeParamList? "(" ParamList? ")" ( "->" TypeExpr )? Block
TypestateOnHandler ::= "on" TypeExpr TypestateHandlerParams? TypestateHandlerProvenance? TypestateHandlerReturn? Block
TypestateHandlerParams ::= "(" TypestateHandlerParamList? ")"
TypestateHandlerParamList ::= TypestateHandlerParam ("," TypestateHandlerParam)* ","?
TypestateHandlerParam ::= Param | Identifier
TypestateHandlerProvenance ::= "for" TypeExpr ( ":" Identifier )? "(" Identifier ")"
TypestateHandlerReturn ::= "->" (TypeExpr | "stay")
TypestateState     ::= "state" Identifier "{" TypestateStateItem* "}"
TypestateStateItem ::= TypestateStateFields | TypestateStateMethod | TypestateOnHandler
TypestateStateFields ::= "fields" "{" StructFieldList? "}"
TypestateStateMethod ::= Func

# Type Expressions
TypeExpr           ::= TypeExprTerm ( "|" TypeExprTerm )*
TypeExprTerm       ::= TypeExprAtom RefinementType? TypeSuffix*
TypeExprAtom       ::= UnitType | NamedType | TupleType | RangeType | FnType
TypeSuffix         ::= ArraySuffix | SliceSuffix | HeapSuffix
ArraySuffix        ::= "[" IntLitList "]"
SliceSuffix        ::= "[" "]"
HeapSuffix         ::= "^"
RefinementType     ::= ":" Refinement ( "&" Refinement )*

UnitType           ::= "()"
NamedType          ::= Identifier TypeArgList?
TupleType          ::= "(" TypeExpr "," ( TypeExprList )? ")"
RangeType          ::= "range" "(" IntLit ("," IntLit)? ")"
FnType             ::= "fn" "(" FnTypeParamList? ")" "->" TypeExpr
Refinement         ::= "bounds" "(" IntLit ("," IntLit)? ")" | "nonzero"

FnTypeParamList    ::= FnTypeParam ("," FnTypeParam)* ","?
FnTypeParam        ::= ParamMode? TypeExpr

TypeExprList       ::= TypeExpr ( "," TypeExpr )* ","?
IntLitList         ::= IntLit ("," IntLit)* ","?

# Functions
FuncDecl           ::= FuncSig ";"
Func               ::= FuncSig Block

FuncSig            ::= "fn" Identifier TypeParamList? "(" ParamList? ")" ( "->" TypeExpr )?

# Methods
MethodBlock        ::= Identifier "::" Identifier? "{" MethodItem* "}"
MethodItem         ::= MethodDecl | MethodDef | PropertyDef
MethodDecl         ::= AttributeList MethodSig ";"
MethodDef          ::= AttributeList MethodSig Block
MethodSig          ::= "fn" Identifier TypeParamList? "(" SelfParam ("," ParamList)? ")" ( "->" TypeExpr )?

TypeParamList      ::= "<" IdentifierList ">"
IdentifierList     ::= Identifier ( "," Identifier )* ","?
TypeArgList        ::= "<" TypeExprList ">"
SelfParam          ::= ParamMode? "self"

# Properties
PropertyDef        ::= "prop" Identifier ":" TypeExpr "{" PropertyAccessor* "}"
PropertyAccessor   ::= "get" Block | "set" "(" Identifier ")" Block

# Closures
ClosureSig         ::= "|" ParamList? "|" ( "->" TypeExpr )?
ClosureCaptureList ::= "[" "move" Identifier ( "," Identifier )* ","? "]"

# Parameters (common)
ParamList          ::= Param ("," Param)* ","?
Param              ::= ParamMode? Identifier ":" TypeExpr
ParamMode          ::= "inout" | "out" | "sink"

# Blocks
Block              ::= "{" BlockItem* (Expr ";"?)? "}"
BlockItem          ::= StmtExpr | Expr ";"

# Statement Expressions
StmtExpr           ::= LetBind | VarBind | VarDecl | Assign
                     | While | For | Break | Continue | Return

# Variable Binding/Declaration
LetBind            ::= "let" Pattern ( ":" TypeExpr )? "=" Expr ";"
VarBind            ::= "var" Pattern ( ":" TypeExpr )? "=" Expr ";"
VarDecl            ::= "var" Identifier ":" TypeExpr ";"

# Assignment
Assign             ::= PostfixExpr AssignOp Expr ";"
AssignOp           ::= "=" | "+=" | "-=" | "*=" | "/=" | "%="
                     | "&=" | "|=" | "^=" | "<<=" | ">>="

# Loops
While              ::= "while" Expr Block
For                ::= "for" Pattern "in" (RangeExpr | Expr) Block

# Control flow (statements)
Break              ::= "break" ";"
Continue           ::= "continue" ";"
Return             ::= "return" Expr? ";"

RangeExpr          ::= IntLit ".." IntLit

# Patterns
Pattern            ::= IdentPattern | ArrayPattern | TuplePattern | StructPattern
IdentPattern       ::= Identifier
ArrayPattern       ::= "[" PatternList "]"
TuplePattern       ::= "(" Pattern "," PatternList? ")"
StructPattern      ::= Identifier "{" StructPatternFieldList? "}"

StructPatternFieldList ::= StructPatternField ("," StructPatternField)* ","?
StructPatternField ::= Identifier ( ":" Pattern )?

PatternList        ::= Pattern ( "," Pattern )* ","?

# Expressions
Expr               ::= If | Match | InfixExpr

# Control flow
If                 ::= "if" Expr Block IfTail?
IfTail             ::= "else" (Block | If)

Match              ::= "match" Expr "{" MatchArm ("," MatchArm )* ","? "}"
MatchArm           ::= MatchPattern "=>" Expr
MatchPattern       ::= "_" | BoolLit | IntLit | TypedMatchBinding
                     | TuplePattern | EnumVariantPattern
TypedMatchBinding  ::= Identifier ":" TypeExpr

EnumVariantPattern ::= Identifier ("::" Identifier)? ( "(" MatchBindingList? ")" )?
MatchBindingList   ::= MatchBinding ("," MatchBinding)* ","?

TuplePattern       ::= "(" TuplePatternElemList? ")"
TuplePatternElemList ::= TuplePatternElem ("," TuplePatternElem)* ","?
TuplePatternElem   ::= MatchBinding | TuplePattern

MatchBinding       ::= Identifier | "_"

# Operator Precedence (Lowest to Highest)
# 0. Logical OR (||)
# 1. Logical AND (&&)
# 2. Bitwise OR (|)
# 3. Bitwise XOR (^)
# 4. Bitwise AND (&)
# 5. Comparison (==, !=, <, <=, >, >=)
# 6. Shift (<<, >>)
# 7. Additive (+, -)
# 8. Multiplicative (*, /, %)
# 9. Unary (-, !, ~)
# 10. Postfix

InfixExpr          ::= OrExpr

OrExpr             ::= AndExpr ( "||" AndExpr )*
AndExpr            ::= BitOrExpr ( "&&" BitOrExpr )*
BitOrExpr          ::= BitXorExpr ( "|" BitXorExpr )*
BitXorExpr         ::= BitAndExpr ( "^" BitAndExpr )*
BitAndExpr         ::= CompareExpr ( "&" CompareExpr )*
CompareExpr        ::= ShiftExpr ( ( "==" | "!=" | "<" | "<=" | ">" | ">=" ) ShiftExpr )*
ShiftExpr          ::= AddExpr ( ( "<<" | ">>" ) AddExpr )*
AddExpr            ::= MulExpr ( ( "+" | "-" ) MulExpr )*
MulExpr            ::= UnaryExpr ( ( "*" | "/" | "%" ) UnaryExpr )*
UnaryExpr          ::= ( "-" | "!" | "~" | "^" ) UnaryExpr
                     | "move" UnaryExpr
                     | PostfixExpr

PostfixExpr        ::= Primary ( Call | LabeledCall | ArrayIndex | SliceRange | TupleField | StructField | MethodCall )*

Call               ::= "(" CallArgList? ")"
LabeledCall        ::= ":" Identifier "(" CallArgList? ")"
CallArgList        ::= CallArg ("," CallArg)* ","?
CallArg            ::= ( "inout" | "out" | "move" )? Expr

ArrayIndex         ::= "[" ExprList "]"
SliceRange         ::= "[" Expr? ".." Expr? "]"
TupleField         ::= "." IntLit
StructField        ::= "." Identifier
MethodCall         ::= "." Identifier "(" CallArgList? ")"

Primary            ::= Literal
                     | Identifier
                     | EnumVariant
                     | StructUpdate
                     | EmitExpr
                     | ReplyExpr
                     | "(" Expr ")"
                     | Block
                     | ClosureExpr

EmitExpr           ::= "emit" ("Send" | ("Request" ( ":" Identifier )?)) "(" "to" ":" Expr "," Expr ")"
ReplyExpr          ::= "reply" "(" Expr "," Expr ")"

ClosureExpr        ::= ClosureCaptureList? ClosureSig Expr

Literal            ::= UnitLit | IntLit | BoolLit | CharLit | StringLit | StringFmt
                     | ArrayLit | SetLit | TupleLit | StructLit

ArrayLit           ::= TypeExpr? "[" ExprList "]"
                     | TypeExpr? "[" Expr ";" IntLit "]"
TupleLit           ::= "(" Expr "," ExprList? ")"
SetLit             ::= "{" Expr "," ExprList? "}"

StructLit          ::= Identifier "{" StructLitFieldList? "}"
StructLitFieldList ::= StructLitField ("," StructLitField)* ","?
StructLitField     ::= Identifier ":" Expr

StructUpdate       ::= "{" Expr "|" StructUpdateField ("," StructUpdateField)* ","? "}"
StructUpdateField  ::= Identifier ":" Expr

EnumVariant        ::= Identifier "::" Identifier ( "(" ExprList ")" )?

# Common
ExprList           ::= Expr ( "," Expr )* ","?

# Terminals
UnitLit            ::= "()"
BoolLit            ::= "true" | "false"
IntLit             ::= DecimalLit | BinaryLit | OctalLit | HexLit
DecimalLit         ::= [0-9] ([0-9_])*
BinaryLit          ::= "0b" [01_]+
OctalLit           ::= "0o" [0-7_]+
HexLit             ::= "0x" [0-9a-fA-F_]+
CharLit            ::= '\'' (char | escape) '\''
StringLit          ::= '"' (string-char | escape)* '"'
StringFmt          ::= "f" StringLit

Identifier         ::= [a-zA-Z_][a-zA-Z0-9_]*
