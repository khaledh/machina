# Machina programming language grammar

# Top-level
Module             ::= Decl*

Decl               ::= Function | TypeDecl

Function           ::= "fn" Identifier "(" FunctionParamList? ")" ( "->" TypeExpr )? Block
FunctionParamList  ::= FunctionParam ("," FunctionParam)* ","?
FunctionParam      ::= Identifier ":" TypeExpr

# Type Declarations
TypeDecl           ::= "type" Identifier "=" TypeDeclBody
TypeDeclBody       ::= TypeAliasDef
                     | StructDef
                     | EnumDef

TypeAliasDef       ::=  TypeExpr ";"?

StructDef          ::= "{" StructFieldList? "}"
StructFieldList    ::= StructField ("," StructField)* ","?
StructField        ::= Identifier ":" TypeExpr

EnumDef            ::= EnumVariantDef ("|" EnumVariantDef)* ";"?
EnumVariantDef     ::= Identifier

# Type Expressions
TypeExpr           ::= NamedType | ArrayType | TupleType
NamedType          ::= "()" | Identifier
ArrayType          ::= TypeExpr "[" Integer ("," Integer)* ","? "]"
TupleType          ::= "(" TypeExpr "," ( TypeExpr ( "," TypeExpr )* )? ","? ")"

# Blocks
Block              ::= "{" (Expr ";")* Expr? "}"

# Expressions
Expr               ::= Let
                     | Var
                     | Assign
                     | If
                     | While
                     | InfixExpr

# Binding
LetBind            ::= "let" Pattern ( ":" TypeExpr )? "=" Expr
VarBind            ::= "var" Pattern ( ":" TypeExpr )? "=" Expr

# Patterns
Pattern            ::= IdentPattern | ArrayPattern | TuplePattern | StructPattern
IdentPattern       ::= Identifier
ArrayPattern       ::= "[" PatternList "]"
TuplePattern       ::= "(" Pattern "," PatternList? ")"
StructPattern      ::= Identifier "{" StructPatternFieldList? "}"

StructPatternFieldList ::= StructPatternField ("," StructPatternField)* ","?
StructPatternField ::= Identifier ( ":" Pattern )?

PatternList        ::= Pattern ( "," Pattern )* ","?

# Assignment
Assign             ::= PostfixExpr "=" Expr

# Control flow
If                 ::= "if" Expr Block ( "else" Block )?
While              ::= "while" Expr Block

# Operator Precedence (Lowest to Highest)
# 1. Comparison (==, !=, <, <=, >, >=)
# 2. Additive (+, -)
# 3. Multiplicative (*, /)
# 4. Unary (-, !)
# 5. Postfix (Call, ArrayIndex, TupleField, StructField)

InfixExpr          ::= CompareExpr

CompareExpr        ::= AddExpr ( ( "==" | "!=" | "<" | "<=" | ">" | ">=" ) AddExpr )*
AddExpr            ::= MulExpr ( ( "+" | "-" ) MulExpr )*
MulExpr            ::= UnaryExpr ( ( "*" | "/" ) UnaryExpr )*

UnaryExpr          ::= ( "-" | "!" ) UnaryExpr 
                     | PostfixExpr

PostfixExpr        ::= Primary ( Call | ArrayIndex | TupleField | StructField )*

Call               ::= "(" ExprList? ")"
ArrayIndex         ::= "[" ExprList? "]"
TupleField         ::= "." Integer
StructField        ::= "." Identifier

Primary            ::= Literal
                     | Identifier
                     | EnumVariant
                     | "(" Expr ")"
                     | Block

Literal            ::= Unit | Integer | Boolean | ArrayLit | TupleLit | StructLit

ArrayLit           ::= "[" ExprList "]"
TupleLit           ::= "(" Expr "," ExprList? ")"

ExprList           ::= Expr ( "," Expr )* ","?

StructLit          ::= Identifier "{" StructLitFieldList? "}"
StructLitFieldList ::= StructLitField ("," StructLitField)* ","?
StructLitField     ::= Identifier ":" Expr

EnumVariant        ::= Identifier "::" Identifier

# Terminals
Unit               ::= "()"
Integer            ::= [0-9]+
Boolean            ::= "true" | "false"
Identifier         ::= [a-zA-Z_][a-zA-Z0-9_]*
