# Machina programming language grammar

# Top-level
Module             ::= Decl*

Decl               ::= Function | TypeAlias

Function           ::= "fn" Identifier "(" FunctionParamList? ")" ( "->" TypeExpr )? Block
FunctionParamList  ::= FunctionParam ("," FunctionParam)* ","?
FunctionParam      ::= Identifier ":" TypeExpr

TypeAlias          ::= "type" Identifier "=" TypeExpr ";"

# Type Expressions
TypeExpr           ::= NamedType | ArrayType | TupleType
NamedType          ::= "()" | Identifier
ArrayType          ::= TypeExpr "[" Integer ("," Integer)* "]"
TupleType          ::= "(" TypeExpr "," ( TypeExpr ( "," TypeExpr )* )? ","? ")"

# Blocks
Block              ::= "{" (Expr ";")* Expr? "}"

# Expressions
Expr               ::= Let
                     | Var
                     | Assign
                     | If
                     | While
                     | InfixExpr

# Binding
Let                ::= "let" Pattern ( ":" TypeExpr )? "=" Expr
Var                ::= "var" Pattern ( ":" TypeExpr )? "=" Expr

# Patterns
Pattern            ::= IdentPattern | ArrayPattern | TuplePattern
IdentPattern       ::= Identifier
ArrayPattern       ::= "[" Pattern ( "," Pattern )* ","? "]"
TuplePattern       ::= "(" Pattern "," ( Pattern ( "," Pattern )* )? ","? ")"

# Assignment
Assign             ::= PostfixExpr "=" Expr

# Control flow
If                 ::= "if" Expr Block ( "else" Block )?
While              ::= "while" Expr Block

# Operator Precedence (Lowest to Highest)
# 1. Comparison (==, !=, <, <=, >, >=)
# 2. Additive (+, -)
# 3. Multiplicative (*, /)
# 4. Unary (-, !)
# 5. Postfix (Call, Index)

InfixExpr          ::= CompareExpr

CompareExpr        ::= AddExpr ( ( "==" | "!=" | "<" | "<=" | ">" | ">=" ) AddExpr )*
AddExpr            ::= MulExpr ( ( "+" | "-" ) MulExpr )*
MulExpr            ::= UnaryExpr ( ( "*" | "/" ) UnaryExpr )*

UnaryExpr          ::= ( "-" | "!" ) UnaryExpr 
                     | PostfixExpr

PostfixExpr        ::= Primary ( Call | Index | TupleFieldAccess )*

Call               ::= "(" Expr ("," Expr)* ","? ")"
Index              ::= "[" Expr ("," Expr)* ","? "]"
TupleFieldAccess   ::= "." Integer

Primary            ::= Unit
                     | Integer
                     | Boolean
                     | ArrayLit
                     | TupleLit
                     | Identifier
                     | "(" Expr ")"
                     | Block

ArrayLit           ::= "[" ( Expr ( "," Expr )* ","? )? "]"
TupleLit           ::= "(" Expr "," ( Expr ( "," Expr )* )? ","? ")"

# Terminals
Unit               ::= "()"
Integer            ::= [0-9]+
Boolean            ::= "true" | "false"
Identifier         ::= [a-zA-Z_][a-zA-Z0-9_]*
