# Machina programming language grammar

# Top-level
Module             ::= Function*

Function           ::= "fn" Identifier "(" FunctionParamList? ")" ( "->" Type )? Block

FunctionParamList  ::= FunctionParam ("," FunctionParam)* ","?
FunctionParam      ::= Identifier ":" Type

# Types
Type               ::= SimpleType | CompoundType
SimpleType         ::= "()" | "u64" | "bool"
CompoundType       ::= ArrayType
ArrayType          ::= Type "[" Integer ("," Integer)* "]"

# Blocks
Block              ::= "{" (StmtOrExpr ";")* Expr? "}"

StmtOrExpr         ::= Let
                     | Var
                     | Assign
                     | Expr

Let                ::= "let" Pattern "=" Expr
Var                ::= "var" Pattern ( ":" Type )? "=" Expr
Assign             ::= PostfixExpr "=" Expr

# Patterns
Pattern            ::= IdentPattern | ArrayPattern
IdentPattern       ::= Identifier
ArrayPattern       ::= "[" Pattern ( "," Pattern )* ","? "]"

# Expressions
Expr               ::= If | While | InfixExpr

If                 ::= "if" Expr Block ( "else" Block )?
While              ::= "while" Expr Block

# Operator Precedence (Lowest to Highest)
# 1. Comparison (==, !=, <, <=, >, >=)
# 2. Additive (+, -)
# 3. Multiplicative (*, /)
# 4. Unary (-, !)
# 5. Postfix (Call, Index)

InfixExpr          ::= CompareExpr

CompareExpr        ::= AddExpr ( ( "==" | "!=" | "<" | "<=" | ">" | ">=" ) AddExpr )*
AddExpr            ::= MulExpr ( ( "+" | "-" ) MulExpr )*
MulExpr            ::= UnaryExpr ( ( "*" | "/" ) UnaryExpr )*

UnaryExpr          ::= ( "-" | "!" ) UnaryExpr 
                     | PostfixExpr

PostfixExpr        ::= Primary ( Call | Index )*

Call               ::= "(" Expr ("," Expr)* ","? ")"
Index              ::= "[" Expr ("," Expr)* ","? "]"

Primary            ::= Unit
                     | Integer
                     | Boolean
                     | ArrayLit
                     | Identifier
                     | "(" Expr ")"
                     | Block

ArrayLit           ::= "[" ( Expr ( "," Expr )* ","? )? "]"

# Terminals
Unit               ::= "()"
Integer            ::= [0-9]+
Boolean            ::= "true" | "false"
Identifier         ::= [a-zA-Z_][a-zA-Z0-9_]*
