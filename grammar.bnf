# Machina programming language grammar

# Top-level
Module             ::= Decl*

Decl               ::= TypeDecl | Function

# Type Declarations
TypeDecl           ::= "type" Identifier "=" TypeDeclBody
TypeDeclBody       ::= TypeAliasDef
                     | StructDef
                     | EnumDef

TypeAliasDef       ::=  TypeExpr ";"?

StructDef          ::= "{" StructFieldList? "}"
StructFieldList    ::= StructField ("," StructField)* ","?
StructField        ::= Identifier ":" TypeExpr

EnumDef            ::= EnumVariantDef ("|" EnumVariantDef)* ";"?
EnumVariantDef     ::= Identifier ( "(" TypeExprList ")" )?

# Type Expressions
TypeExpr           ::= UnitType | NamedType | ArrayType | TupleType | RangeType

UnitType           ::= "()"
NamedType          ::= Identifier
ArrayType          ::= TypeExpr "[" IntLitList "]"
TupleType          ::= "(" TypeExpr "," ( TypeExprList )? ")"
RangeType          ::= "range" "(" IntLit ("," IntLit)? ")"

TypeExprList       ::= TypeExpr ( "," TypeExpr )* ","?
IntLitList         ::= IntLit ("," IntLit)* ","?

# Functions
Function           ::= "fn" Identifier "(" FunctionParamList? ")" ( "->" TypeExpr )? Block
FunctionParamList  ::= FunctionParam ("," FunctionParam)* ","?
FunctionParam      ::= Identifier ":" TypeExpr

# Blocks
Block              ::= "{" BlockItem* (Expr ";"?)? "}"
BlockItem          ::= StmtExpr | Expr ";"

# Statement Expressions
StmtExpr           ::= LetBind | VarBind | Assign | While | For

# Binding
LetBind            ::= "let" Pattern ( ":" TypeExpr )? "=" Expr ";"
VarBind            ::= "var" Pattern ( ":" TypeExpr )? "=" Expr ";"

# Assignment
Assign             ::= PostfixExpr "=" Expr ";"

# Loops
While              ::= "while" Expr Block
For                ::= "for" Pattern "in" (RangeExpr | Expr) Block

RangeExpr          ::= IntLit ".." IntLit

# Patterns
Pattern            ::= IdentPattern | ArrayPattern | TuplePattern | StructPattern
IdentPattern       ::= Identifier
ArrayPattern       ::= "[" PatternList "]"
TuplePattern       ::= "(" Pattern "," PatternList? ")"
StructPattern      ::= Identifier "{" StructPatternFieldList? "}"

StructPatternFieldList ::= StructPatternField ("," StructPatternField)* ","?
StructPatternField ::= Identifier ( ":" Pattern )?

PatternList        ::= Pattern ( "," Pattern )* ","?

# Expressions
Expr               ::= If | Match | InfixExpr

# Control flow
If                 ::= "if" Expr Block ( "else" Block )?

Match              ::= "match" Expr "{" MatchArm ("," MatchArm )* ","? "}"
MatchArm           ::= MatchPattern "=>" Expr
MatchPattern       ::= "_" | EnumVariantPattern
EnumVariantPattern ::= Identifier ("::" Identifier)? ( "(" IdentList? ")" )?
IdentList          ::= Identifier ("," Identifier)* ","?

# Operator Precedence (Lowest to Highest)
# 1. Comparison (==, !=, <, <=, >, >=)
# 2. Additive (+, -)
# 3. Multiplicative (*, /)
# 4. Unary (-, !)
# 5. Postfix (Call, ArrayIndex, TupleField, StructField)

InfixExpr          ::= CompareExpr

CompareExpr        ::= AddExpr ( ( "==" | "!=" | "<" | "<=" | ">" | ">=" ) AddExpr )*
AddExpr            ::= MulExpr ( ( "+" | "-" ) MulExpr )*
MulExpr            ::= UnaryExpr ( ( "*" | "/" ) UnaryExpr )*

UnaryExpr          ::= ( "-" | "!" ) UnaryExpr 
                     | PostfixExpr

PostfixExpr        ::= Primary ( Call | ArrayIndex | TupleField | StructField )*

Call               ::= "(" ExprList? ")"
ArrayIndex         ::= "[" ExprList? "]"
TupleField         ::= "." IntLit
StructField        ::= "." Identifier

Primary            ::= Literal
                     | Identifier
                     | EnumVariant
                     | StructUpdate
                     | "(" Expr ")"
                     | Block

Literal            ::= UnitLit | IntLit | BoolLit | CharLit | StringLit
                     | ArrayLit | TupleLit | StructLit

ArrayLit           ::= "[" ExprList "]"
TupleLit           ::= "(" Expr "," ExprList? ")"

StructLit          ::= Identifier "{" StructLitFieldList? "}"
StructLitFieldList ::= StructLitField ("," StructLitField)* ","?
StructLitField     ::= Identifier ":" Expr

StructUpdate       ::= "{" Expr "|" StructUpdateField ("," StructUpdateField)* ","? "}"
StructUpdateField  ::= Identifier ":" Expr

EnumVariant        ::= Identifier "::" Identifier ( "(" ExprList ")" )?

# Common
ExprList           ::= Expr ( "," Expr )* ","?

# Terminals
UnitLit            ::= "()"
BoolLit            ::= "true" | "false"
IntLit             ::= [0-9]+
CharLit            ::= '\'' (ascii-char | escape) '\''
StringLit          ::= '"' (string-char | escape)* '"'

Identifier         ::= [a-zA-Z_][a-zA-Z0-9_]*
